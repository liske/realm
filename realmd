#!/usr/bin/perl

# realm - REmote Authentication Log Monitor
#
# Authors:
#   Thomas Liske <thomas@fiasko-nw.net>
#
# Copyright Holder:
#   2014 (C) Thomas Liske [http://fiasko-nw.net/~thomas/]
#
# License:
#   This program is free software; you can redistribute it and/or modify
#   it under the terms of the GNU General Public License as published by
#   the Free Software Foundation; either version 2 of the License, or
#   (at your option) any later version.
#

use warnings;
use strict;

use AnyEvent;
use AnyEvent::Handle;
use AnyEvent::Socket;
use Getopt::Std;
use Log::Any::Adapter;
use Log::Dispatch;
use Log::Dispatch::Screen;
use Log::Dispatch::Syslog;
use Proc::Daemon;
use Proc::PID::File;
use Tie::Cache;

my $VERSION = qq(0.1);
$Getopt::Std::STANDARD_HELP_VERSION++;

sub HELP_MESSAGE {
    print <<USG;
Usage:

  realm-feed [-v] [-c <cfg>]

    -v		be more verbose
    -c <cfg>	config filename

USG
}

sub VERSION_MESSAGE {
    print <<LIC;

realm $VERSION - REmote Authentication Log Monitor

Authors:
  Thomas Liske <thomas\@fiasko-nw.net>

Copyright Holder:
  2014 (C) Thomas Liske [http://fiasko-nw.net/~thomas/]

Upstream:
  https://github.com/liske/realm

This program is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 2 of the License, or
(at your option) any later version.

LIC
#/
}


our %realmconf = (
    listen_port => 1712,
    listen_addr => '::1',
);
our %realm_skeys;
our @realm_dkeys;
our %realms = ();

our $opt_c = '/etc/realm/realm.conf';
our $opt_v;
our $opt_f;
unless(getopts('c:vf')) {
    HELP_MESSAGE;
    exit 1;
}

die "ERROR: Could not read config file '$opt_c'!\n" unless(-r $opt_c);

# slurp config file
eval `cat "$opt_c"`;
die "$@\n" if($@);


$0 = 'realmd';
my $logfmt = sub { my %p = @_; chomp($p{'message'}); return ("[$p{level}] $p{message}\n"); };
my $logger = Log::Dispatch->new(callbacks => $logfmt);
$logger->add(
    Log::Dispatch::Screen->new(
	name => 'console',
	min_level => ($opt_f ? ($opt_v ? 'info' : 'notice') : 'warning'),
    )
);
$logger->add(
    Log::Dispatch::Syslog->new(
	name => 'syslog',
	min_level => ($opt_v ? 'info' : 'notice'),
	ident => 'realmd',
	logopt => 'pid',
	facility => 'daemon',
    )
) unless($opt_f);
Log::Any::Adapter->set('Dispatch', dispatcher => $logger);


sub WARN_handler {
    my($signal) = @_;

    if(defined $logger) {
	$logger->warning("WARNING: $signal");
    }
    else {
	printf STDERR "WARNING: $signal\n";
    }
}

sub DIE_handler {
    my($signal) = @_;

    if(defined $logger) {
	$logger->critical("ERROR: $signal");
    }
    else {
	printf STDERR "ERROR: $signal\n";
    }
    
    exit 1;
}

sub INT_handler {
    if(defined $logger) {
	$logger->notice('Got SIGINT, terminating.');
    }
    else {
	printf STDERR "Got SIGINT, terminating.\n";
    }
    
    exit;
}

$SIG{__WARN__} = 'WARN_handler';
$SIG{__DIE__}  = 'DIE_handler';
$SIG{INT} = 'INT_handler';
$SIG{PIPE} = 'IGNORE';
$SIG{CHLD} = 'IGNORE';

END {
    my $m = "terminated (rc=$?)";
    if(defined $logger) {
	$logger->notice($m);
    }
    else {
	print STDERR "$m\n";
    }
}

# prepare dynamic field cache
my %caches;
foreach my $dkey (@realm_dkeys) {
    next unless(exists($dkey->{cache}));

    foreach my $field (@{ $dkey->{keys} }) {
	my %_cache = ();
	tie %_cache, 'Tie::Cache', { MaxBytes => 4*10**6, WriteSync => 0, Debug => 2 };
	$caches{$dkey->{cache}->{name} . '#' . $field} = \%_cache;
    }
}

sub update_tree($$@) {
    my $dataset = shift;
    my $tree = shift;
    my $key = shift;
    my @keys = @_;

    unless(exists($tree->{$dataset->{$key}})) {
	$tree->{$dataset->{$key}}->{childs} = { };
	$tree->{$dataset->{$key}}->{count} = 0;
    }

    $tree->{$dataset->{$key}}->{count}++;

    if($tree->{$dataset->{$key}}->{count} > 0x80000000) {
	foreach my $v (keys %$tree) {
	    $tree->{$v}->{p} = $tree->{$v}->{count} >> 2;
	}
    }

    &update_tree($dataset, $tree->{$dataset->{$key}}->{childs}, @keys) if(scalar @keys);
}

my %instances;
my %conns;
my $sock = tcp_server $realmconf{'listen_addr'}, $realmconf{'listen_port'}, sub {
    my ($fh, $host, $port) = @_;

    $conns{$fh} = new AnyEvent::Handle(
	fh => $fh,
	on_eof => sub {
	    delete($conns{$fh});
	    exit;
	},
	on_read => sub {
	    $conns{$fh}->push_read(json => sub {
		shift;
		my $dataset = shift;

		# ignore unkown instances
		return unless(exists($dataset->{instance}) && exists($realms{$dataset->{instance}}));

		# eval dynamic fields
		foreach my $dkey (@realm_dkeys) {
		    my $found = 0;
		    my $ckey;
		    if(exists($dkey->{cache})) {
			my $cinfo = $dkey->{cache};
			$ckey = &{$cinfo->{key}}($dataset);

			$found++;
			foreach my $field (@{ $dkey->{keys} }) {
			    if(exists($caches{$cinfo->{name} . '#' . $field}->{$ckey})) {
				$dataset->{$field} = $caches{$cinfo->{name} . '#' . $field}->{$ckey};
			    }
			    else {
				$found = 0;
				last;
			    }
			}
		    }

		    unless($found) {
			&{$dkey->{sub}}($dataset);
			
			if($ckey) {
			    foreach my $field (@{ $dkey->{keys} }) {
				$caches{$dkey->{cache}->{name} . '#' . $field}->{$ckey} = $dataset->{$field};
			    }
			}
		    }
		}

		# apply filters
		foreach my $key (keys %{ $realms{$dataset->{instance}}->{filters} }) {
		    return unless($dataset->{$key} =~ /$realms{$dataset->{instance}}->{filters}->{$key}/);
		}

		my $root = join('#', map { $dataset->{$_}; } @{ $realms{$dataset->{instance}}->{index} });
		foreach my $tree (keys %{ $realms{$dataset->{instance}}->{trees} }) {
		    $instances{$dataset->{instance}}->{$tree}->{$root} = {} unless(exists($instances{$dataset->{instance}}->{$tree}->{$root}));
		    update_tree($dataset, $instances{$dataset->{instance}}->{$tree}->{$root}, @{ $realms{$dataset->{instance}}->{trees}->{$tree}->{keys} });
		}

		use Data::Dumper;
		print Dumper($instances{$dataset->{instance}});
	    });
	}
    );
};


$logger->info("enter event loop...");
AnyEvent->condvar->recv;
